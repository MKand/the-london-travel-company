resource "random_password" "db_password" {
  length           = 16
  special          = true
  override_special = "$&*()-_=+[]{}<>?"
}

resource "google_secret_manager_secret" "db_password_secret" {
  secret_id = "db-password"
  replication {
    auto {}
  }
  depends_on = [ google_project_service.enable_apis ]
}

# Add the password generated by Terraform to that Secret
resource "google_secret_manager_secret_version" "db_password_version" {
  secret      = google_secret_manager_secret.db_password_secret.id
  secret_data = random_password.db_password.result
}

resource "google_sql_database_instance" "postgres_db" {
  name             = "london-travel-db-instance"
  region           = var.gcp_region
  database_version = "POSTGRES_17"
  root_password    = random_password.db_password.result
  settings {
    tier    = "db-f1-micro"
    edition = "ENTERPRISE"
  }
  # set `deletion_protection` to true, will ensure that one cannot accidentally delete this instance by
  # use of Terraform whereas `deletion_protection_enabled` flag protects this instance at the GCP level.
  deletion_protection = false
  depends_on = [ google_project_service.enable_apis ]
}

resource "google_sql_database" "london_activities" {
  name     = "london_activities"
  instance = google_sql_database_instance.postgres_db.name
}
